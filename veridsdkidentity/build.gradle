apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.artifactory'

def getArtifactId = { ->
    return "identity"
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion 29
        versionCode rootProject.generateVersionCode()
        versionName rootProject.generateVersionName()
        archivesBaseName = getArtifactId()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

}

artifactory {
    contextUrl = verid_artifactory_url
    //The base Artifactory URL if not overridden by the publisher/resolver
    publish {
        repository {
            repoKey = verid_artifactory_repo
            username = verid_artifactory_username
            password = verid_artifactory_password
            maven = true
        }
        defaults {
            publications('veridsdkidentity')
            publishArtifacts = true
        }
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/AppliedRecognition/Ver-ID-SDK-Identity-Android")
            credentials {
                username = githubUsername
                password = githubAccessToken
            }
        }
    }
    publications {
        veridsdkidentity(MavenPublication) {
            groupId "com.appliedrec.verid"
            version rootProject.generateVersionName()
            artifactId getArtifactId()
            artifact("$buildDir/outputs/aar/${getArtifactId()}-release.aar") {
                extension "aar"
            }
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                def appcompatNode = dependenciesNode.appendNode('dependency')
                appcompatNode.appendNode('groupId', 'androidx.appcompat')
                appcompatNode.appendNode('artifactId', 'appcompat')
                appcompatNode.appendNode('version', rootProject.appcompatVersion)
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "androidx.appcompat:appcompat:$rootProject.appcompatVersion"
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
